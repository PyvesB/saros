/*
  This build applies our picocontainer patches and creates a new picocontainer jar with a different package naming
  instead of the package structure org.picocontainer the package name saros.org.picocontainer is used.
  
  Even if this sub-project is integrated in the whole build process (via the setting.gradle). It is intended to configure
  this project only in this file and not in the parent build.gradle (in the repository root).
  
  This is required, because intellij uses another picocontainer version and this results in conflicts during build.
  The previous solution was to exclude the picocontainer jar from the intellij installation (with a hidden feature
  of the gradle/intellij plugin) this feature will be removed.
*/

buildscript {
  repositories {
      jcenter()
  }
  dependencies {
      classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.3'
  }
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'

version = '2.11.2'

repositories {
  mavenCentral()
}

dependencies {
  compile "org.picocontainer:picocontainer:$version"
}

shadowJar {
  relocate 'org.picocontainer', 'saros.org.picocontainer'
  baseName = 'picocontainer'
  classifier = 'patched_relocated'
  version = version
}
